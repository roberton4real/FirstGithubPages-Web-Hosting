recoveryservicesresources
| where type == "microsoft.recoveryservices/vaults/backupfabrics/protectioncontainers/protecteditems"
| where properties.protectionStatus == "Unhealthy"
| where properties.protectionState == "Protected"
| where properties.lastBackupStatus == "Failed"
| where properties.workloadType in~ ("SQLDataBase")
| where isnotempty(properties.lastBackupTime) and todatetime(properties.lastBackupTime) >= ago(7d)
| where subscriptionId in (
    "00000000-aaaa-bbbb-cccc-111111111111",
    "00000000-aaaa-bbbb-cccc-222222222222",
    "00000000-aaaa-bbbb-cccc-333333333333",
    "00000000-aaaa-bbbb-cccc-444444444444",
    "00000000-aaaa-bbbb-cccc-555555555555",
    "00000000-aaaa-bbbb-cccc-666666666666",
    "00000000-aaaa-bbbb-cccc-777777777777",
    "00000000-aaaa-bbbb-cccc-888888888888"
)
| extend subscriptionName = case(
    subscriptionId == "00000000-aaaa-bbbb-cccc-111111111111", "Subscription A",
    subscriptionId == "00000000-aaaa-bbbb-cccc-222222222222", "Subscription B",
    subscriptionId == "00000000-aaaa-bbbb-cccc-333333333333", "Subscription C",
    subscriptionId == "00000000-aaaa-bbbb-cccc-444444444444", "Subscription D",
    subscriptionId == "00000000-aaaa-bbbb-cccc-555555555555", "Subscription E",
    subscriptionId == "00000000-aaaa-bbbb-cccc-666666666666", "Subscription F",
    subscriptionId == "00000000-aaaa-bbbb-cccc-777777777777", "Subscription G",
    subscriptionId == "00000000-aaaa-bbbb-cccc-888888888888", "Subscription H",
    "Unknown"
)
| extend ResourceLink = strcat('https://portal.azure.com/#@', tenantId, '/resource', id)
| project
    ['Name'] = tostring(properties.friendlyName),
    ['Location'] = location,
    ['Resource Group'] = resourceGroup,
    ['Subscription Name'] = subscriptionName,
    ['Vault Name'] = split(id, "/")[8],
    ['Resource Type'] = tostring(properties.workloadType),  
    ['Last Backup Status'] = tostring(properties.lastBackupStatus),
    ['Protection Status'] = tostring(properties.protectionStatus),  
    ['Last Backup Time'] = todatetime(properties.lastBackupTime),  
    ['Backup Management Type'] = tostring(properties.backupManagementType),  
    ['Vault ID'] = tostring(properties.vaultId),  
    ['Resource Link'] = ResourceLink
| order by ['Last Backup Time'] desc
